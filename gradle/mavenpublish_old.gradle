apply plugin: "com.vanniktech.maven.publish"

mavenPublish {
    // def isLegacyMode = !project.plugins.hasPlugin('org.jetbrains.kotlin.multiplatform')
    def isRelease = isReleaseBuild()
    def signKeyValid = !getGpgKey().isEmpty()

    // useLegacyMode = isLegacyMode
    releaseSigningEnabled = isRelease

    project.logger.warn("publish: maven: release=$isRelease, keyValid=$signKeyValid")

    if (isRelease && !signKeyValid) {
        project.logger.error("Release signing enabled, but key is empty")
    }

    targets {
        installArchives {
            def url = file("${rootProject.buildDir}/localMaven").toURI().toString()
            project.logger.warn("publish: test: repo: $url")
            releaseRepositoryUrl = url
            snapshotRepositoryUrl = url
        }
    }
}

signing {
    def key = getGpgKey()
    def pwd = getGpgPassword()
    if (!key.isEmpty()) {
        useInMemoryPgpKeys(key, pwd)
    }
}

@SuppressWarnings("GrMethodMayBeStatic")
Boolean isReleaseBuild() {
    return !VERSION_NAME.contains("SNAPSHOT")
}

def getGpgKey() {
    return hasProperty('signingKey') ? signingKey : ""
}

def getGpgPassword() {
    return hasProperty('signingPassword') ? signingPassword : ""
}

apply from: "$rootDir/gradle/dokka.gradle"
